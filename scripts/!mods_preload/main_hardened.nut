::Hardened <- {
	ID = "mod_hardened",
	Name = "Hardened",
	Version = "1.21.3",
	GitHubURL = "https://github.com/Darxo/Hardened",
	Temp = {	// Used to globally store variables between function calls to implement more advanced, albeit hacky behaviors
		RootSkillCounter = null,	// This variable will have the SkillCounter of the root skills during the execution of any skill and any delayed executions
	},
	Const = {},
	Global = {},
	Private = {
		IsPreviewingAttackWithHitChance = false,		// If true, then the player is currently previewing an attack with a hitchance
		// Key are full entity scripts
		// Values are Tables with the entries: Reforged and Hardened
		EntityIDFallback = {},
		CustomTacticalScenarios = [],
		LastSpawnedActor = null,
		PersistentData = {},	// Cached copy of persistent data to make readings more performant
	},
}

::Hardened.HooksMod <- ::Hooks.register(::Hardened.ID, ::Hardened.Version, ::Hardened.Name);
::Hardened.HooksMod.require([
	// "vanilla >= 1.5.1-8",
	"mod_reforged >= 0.9.0",
	"mod_dynamic_spawns >= 0.5.0",
]);
::Hardened.HooksMod.conflictWith([
	"mod_heal_repair_fix [Camping Hitpoint Recovery is fixed in Hardened]",
	"mod_RREI [This mods featureset is integrated into Hardened]",
	"EndsBuyback [This mods featureset is integrated into Hardened]",
	"mod_settlement_situations_msu [This mods featureset is integrated into Hardened]",
	"mod_consume [This mods featureset is integrated into Hardened]",
	"mod_deathlog [This mods featureset is integrated into Hardened]",
	"mod_equal_location_scouting [This mods featureset is integrated into Hardened]",
]);

// We need to load after swifter, because we fix a bug there
::Hardened.HooksMod.queue([">mod_reforged", ">mod_swifter", ">mod_crock_pot", ">mod_combat_simulator"], function() {
	::Reforged.Mod.Debug.setFlag("onAnySkillExecutedFully", false);
	::Reforged.Mod.Debug.setFlag("AIAgentFixes", false);

	::Hardened.Mod <- ::MSU.Class.Mod(::Hardened.ID, ::Hardened.Version, ::Hardened.Name);

	::Hardened.Mod.Registry.addModSource(::MSU.System.Registry.ModSourceDomain.GitHub, ::Hardened.GitHubURL);
	::Hardened.Mod.Registry.setUpdateSource(::MSU.System.Registry.ModSourceDomain.GitHub);

	::include("mod_hardened/load");		// Load Hardened-Adjustments and other hooks
	::include("mod_hardened/ui/load");	// Load Hardened JS Adjustments and Hooks

	if (::Hardened.Mod.PersistentData.hasFile("Data"))
	{
		::Hardened.Private.PersistentData = ::Hardened.Mod.PersistentData.readFile("Data");
	}

	// Remove the Fangshire Helmet
	foreach (index, itemScript in ::Const.World.Assets.NewCampaignEquipment)
	{
		if (itemScript == "scripts/items/helmets/legendary/fangshire")
		{
			::Const.World.Assets.NewCampaignEquipment.remove(index);
			break;
		}
	}

	::Hardened.Const.CaravanBannerOffset <- ::createVec(0, 50);
});	// QueueBucket.Normal

::Hardened.HooksMod.queue(">mod_reforged", function() {
	::includeFiles(::IO.enumerateFiles("mod_hardened/api/hooks_early"));
	::includeFiles(::IO.enumerateFiles("mod_hardened/hooks_early"));
}, ::Hooks.QueueBucket.Early);

::Hardened.HooksMod.queue(">mod_reforged", function() {
	::includeFiles(::IO.enumerateFiles("mod_hardened/hooks_late"));

	local isConfigPresent = function()
	{
		foreach (fullPath in ::IO.enumerateFiles("data/"))
		{
			if (fullPath == "data/_config")
			{
				return true;
			}
		}
		return false;
	}

	if (isConfigPresent())
	{
		::logInfo("Hardened: _config.nut was found and will be loaded now");
		try {
			::include("_config.nut");
		}
		catch (_e)
		{
			::logError("_config.nut threw an exception while trying to load");
			::MSU.Log.printData(_e, 2);
		}
	}
}, ::Hooks.QueueBucket.Late);

::Hardened.HooksMod.queue(">mod_reforged", function() {
	::includeFiles(::IO.enumerateFiles("mod_hardened/hooks_last"));
}, ::Hooks.QueueBucket.Last);

::Hardened.HooksMod.queue(">mod_reforged", function() {
	::includeFiles(::IO.enumerateFiles("mod_hardened/hooks_afterhooks"));
}, ::Hooks.QueueBucket.AfterHooks);

::Hardened.HooksMod.queue(">mod_reforged", function() {
	::includeFiles(::IO.enumerateFiles("mod_hardened/hooks_first_world_init"));
}, ::Hooks.QueueBucket.FirstWorldInit);


// Delete all functions in the passed class so that its shell can be repurposed without changing every instance that was pointing to the old script
// @param _functionsToIgnore array of function names that should not be wiped
::Hardened.wipeClass <- function( _classPath, _functionsToIgnore = [] )
{
	::Hardened.HooksMod.rawHook(_classPath, function(p) {
		local toDelete = [];
		foreach (name, func in p)
		{
			if (typeof func == "function")
			{
				if (_functionsToIgnore.find(name) == null)
				{
					toDelete.push(name);
				}
				else
				{
					// ::logWarning(name + " exists in _functionsToIgnore and will be skipped");
				}
			}
		}

		foreach (functionName in toDelete)
		{
			delete p[functionName];
		}
	});
}

// Round a number only if it falls within 0.01 of the next whole number
::Hardened.controlledRound <- function( _value, _tolerance = 0.01)
{
	local roundedValue = ::Math.round(_value);
	if (::fabs(_value - roundedValue) < _tolerance)
	{
		return roundedValue;
	}
	else
	{
		return _value;
	}
}

/// Remove all hooks from the mod _modID that are targeting the script _src
::Hardened.snipeHook <- function( _src, _modID )
{
	if (_src in ::Hooks.BBClass)
	{
		local modIDFound = false;
		for (local i = ::Hooks.BBClass[_src].RawHooks.len() - 1; i >= 0; --i)
		{
			if (::Hooks.BBClass[_src].RawHooks[i].Mod.ID == _modID)
			{
				::Hooks.BBClass[_src].RawHooks.remove(i);
				modIDFound = true;
			}
		}
		if (!modIDFound)
		{
			::logWarning("Warning: modID " + _modID + " was never sniped. You might have misstyped it");
		}
	}
	else
	{
		::logWarning("Warning: Path " + _src + " is never hooked. Hooks from mod " + _modID + " could not be sniped");
	}
}

/// return the first function name in the function caller chain of the function you are currently in, which is not "unkown" (probably because its a low or anonymous function)
/// _skipFunctions allows you to skip this many valid functions
/// @return the name of the caller function, if it exists
/// @return an empty string if the caller function does not exists
/// @info when calling this from within a mockFunction, you must use 1 as argument because there is an additional function inbetween us and our caller there
::Hardened.getFunctionCaller <- function( _skipFunctions = 0 )
{
	// 0 = "getstackinfos"; 1 = "getFunctionCaller"; 2 = whatever function wanted to know its caller
	local currentLevel = 3;

	while (true)
	{
		local stackInfo = ::getstackinfos(currentLevel);
		if (stackInfo == null) return "";

		if (stackInfo.func != "unknown")	// We skip all "unknown" levels by default. Those are probably anonymous/lambda functions or low level functions
		{
			if (_skipFunctions <= 0)
			{
				return stackInfo.func;
			}
			else
			{
				--_skipFunctions;	// We found a valid caller name but we are still tasked to skip those
			}
		}

		++currentLevel;	// We didn't find a sufficient name on this stack level so we go to the next
	}
}

/// [[nodiscard]] Change the behavior of an existing function for a limited number of times
/// @important You should always call the cleanup() of this functions return value once you know you are done
///
///	_object must either be a table or an instance (delegation is allowed) containing the function _functionName somewhere in it
///	_functionName is the name of the function we want to mock
///	_mockedBehavior is a function with the same parameters (including defaults) as _functionName
///		It may return a table with the following two entries
///			"done" (default = false) is a bool signalising if we can start cleaning up
///			"value" (default = not existing) is the custom return value we want the mocked function to return instead. If undefined, then the original function will be called and its return value will be returned. In this case we must make sure to not change the arguments that came by reference, unless that is our intention
///		If the returned table is empty, or nothing is returned (= null), then the default values are used
/// @return object with a:
/// 	cleanup() function, which can be used to manually trigger the cleanup
///		original(...) function, which can be used to manually call the original to trigger its effect or get its return value
///			You must declared the variable for the mockObject in the line before you initiative it with the mockFunction return value, if you want to use mockObject.original in the mockFunction function argument
::Hardened.mockFunction <- function( _object, _functionName, _mockedBehavior )
{
	if (::MSU.isNull(_object))
	{
		if (_object == null)
		{
			::logError("Hardened: mockFunction cannot mock '" + _functionName + "' because the passed _object is null");
		}
		else
		{
			::logError("Hardened: mockFunction cannot mock '" + _functionName + "' because the passed _object is a weakRef which is no longer valid");
		}
		::MSU.Log.printStackTrace();
		throw ::MSU.Exception.InvalidType(_object);
	}

	// A WeakTableRef is an invalid context. It's _get overwrite, will cause globally defined methods/members to no longer be accessible
	local context = _object instanceof ::WeakTableRef ? _object.get() : _object;

	// Find the actual table where the function is defined (if inheritance is at play)
	local functionDefinitionOwner = context;
	while (!::MSU.isIn(_functionName, functionDefinitionOwner))
	{
		if (functionDefinitionOwner instanceof ::WeakTableRef)	// weak table check must be first
		{
			functionDefinitionOwner = functionDefinitionOwner.get();
		}
		else if (typeof functionDefinitionOwner == "table")
		{
			functionDefinitionOwner = functionDefinitionOwner.getdelegate();
		}
		else if (typeof functionDefinitionOwner == "instance")
		{
			functionDefinitionOwner = functionDefinitionOwner.getclass();
		}
		else
		{
			throw ::MSU.Exception.InvalidType(functionDefinitionOwner);
		}
	}

	local oldFunction = functionDefinitionOwner[_functionName];		// Store the original function

	// Mocking
	local hasCleanupHappened = false;
	local cleanupMockedFunction = function()
	{
		if (!hasCleanupHappened)
		{
			hasCleanupHappened = true;
			functionDefinitionOwner[_functionName] = oldFunction;		// Restore the original function
		}
	}

	functionDefinitionOwner[_functionName] = function (...)
	{
		// Some BB Object functions will ignore the custom context and instead use their local one. But that is still correct and fine by us. They usually stop ignoring the custom context the moment, that they are hooked via Modern Hooks
		vargv.insert(0, context);
		local mockResult = _mockedBehavior.acall(vargv);

		if ("done" in mockResult && mockResult.done)	// the mock function signals that it is done and we can begin the clean up process
		{
			cleanupMockedFunction();
		}

		if ("value" in mockResult)
		{
			return mockResult.value;
		}
		else
		{
			return oldFunction.acall(vargv);
		}
	};

	return {
		cleanup = cleanupMockedFunction,
		original = function(...) {
			vargv.insert(0, context);
			return oldFunction.acall(vargv);
		},
	};
}

/// Revert any changes to hitchance during onAnySkillUsed to revert effects like penalty from attacking close targets or generic hitchance differences
/// Remove tooltip about penalty from attacking too close
::Hardened.removeTooClosePenalty <- function( _script )
{
	::Hardened.HooksMod.hook(_script, function(q) {
		q.create = @(__original) function()
		{
			__original();
			this.m.HitChanceBonus = 0;
		}

		q.getTooltip = @(__original) function()
		{
			local ret = __original();

			foreach (index, entry in ret)
			{
				if (entry.text.find("chance to hit targets directly adjacent") != null)
				{
					ret.remove(index);
					break;
				}
			}

			return ret;
		}

		// Revert any changes to hitchance in order to completely remove the penalty from attacking at distance of 1
		// This will also revert skill-specific penalties, so beware of that
		q.onAnySkillUsed = @(__original) function( _skill, _targetEntity, _properties )
		{
			local oldMeleeSkill = _properties.MeleeSkill;
			local oldHitCHanceBonus = this.m.HitChanceBonus;

			__original(_skill, _targetEntity, _properties);

			_properties.MeleeSkill = oldMeleeSkill;
			this.m.HitChanceBonus = oldHitCHanceBonus;
		}
	});
}
