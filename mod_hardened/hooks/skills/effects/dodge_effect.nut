::Hardened.HooksMod.hook("scripts/skills/effects/dodge_effect", function(q) {
	q.m.BaseFraction <- 0.00;
	q.m.FractionPerEmptyTile <- 0.05;

	q.create = @(__original) function()
	{
		__original();
		this.m.Description = "Harness your agility to evade attacks, bolstering your defenses with quick reflexes. The more space you have to move, the harder you are to hit."
		this.m.IconMini = "perk_01_mini";	// Reforged: ""; Vanilla: "perk_01_mini"
	}

	q.onUpdate = @(__original) function( _properties )
	{
		__original(_properties);
		_properties.UpdateWhenTileOccupationChanges = true;	// Because this effect grants defense depending on adjacent empty tiles
	}

	// Overwrite of Vanilla function to stop its effects and apply our own
	q.onAfterUpdate = @() function( _properties )
	{
		this.m.IsHidingIconMini = true;
		if (this.getContainer().getActor().isPlacedOnMap())
		{
			local defenseValue = this.calculateBonus(null, _properties);
			if (defenseValue > 0)
			{
				_properties.MeleeDefense += defenseValue;
				_properties.RangedDefense += defenseValue;
				this.m.IsHidingIconMini = false;	// The mini icon is hidden while fully surrounded or otherwise not gaining any bonus from dodge
			}
		}
	}

	// Overwrite of Vanilla function to stop its effects and apply our own
	q.getTooltip = @() function()
	{
		local ret = this.skill.getTooltip();	// Get name and description the way that the base class does it

		if (this.getContainer().getActor().isPlacedOnMap())
		{
			local defenseValue = this.calculateBonus();
			ret.extend([
				{
					id = 10,
					type = "text",
					icon = "ui/icons/melee_defense.png",
					text = ::Reforged.Mod.Tooltips.parseString(::MSU.Text.colorizeValue(defenseValue, {AddSign = true}) + " [$ $|Concept.MeleeDefense]"),
				},
				{
					id = 11,
					type = "text",
					icon = "ui/icons/ranged_defense.png",
					text = ::Reforged.Mod.Tooltips.parseString(::MSU.Text.colorizeValue(defenseValue, {AddSign = true}) + " [$ $|Concept.RangeDefense]"),
				}
			]);
		}
		else
		{
			local baseBonus = this.calculateBonus(0);
			local bonusPerTile = this.calculateBonus(1) - baseBonus;

			if (this.m.BaseFraction != 0.0)
			{
				ret.push({
					id = 10,
					type = "text",
					icon = "ui/icons/melee_defense.png",
					text = ::Reforged.Mod.Tooltips.parseString("Gain " + ::MSU.Text.colorizePct(this.m.BaseFraction) + " (" + ::MSU.Text.colorPositive(baseBonus) + ") of this character\'s current [$ $|Concept.Initiative] as a bonus to [$ $|Concept.MeleeDefense] and [$ $|Concept.RangeDefense]"),
				});
			}

			if (this.m.FractionPerEmptyTile != 0.0)
			{
				ret.push({
					id = 10,
					type = "text",
					icon = "ui/icons/melee_defense.png",
					text = ::Reforged.Mod.Tooltips.parseString("Gain " + ::MSU.Text.colorizePct(this.m.FractionPerEmptyTile) + " (" + ::MSU.Text.colorPositive(bonusPerTile) + ") of this character\'s current [$ $|Concept.Initiative] as a bonus to [$ $|Concept.MeleeDefense] and [$ $|Concept.RangeDefense] for every adjacent empty tile."),
				});
			}
		}

		return ret;
	}

// private
	q.calculateBonus <- function( _emptyTilesOverwrite = null, _propertiesOverwrite = null )
	{
		local combinedFraction = this.m.BaseFraction;

		if (_emptyTilesOverwrite == null)
		{
			if (this.getContainer().getActor().isPlacedOnMap())
			{
				local myTile = this.getContainer().getActor().getTile();
				foreach (nextTile in ::MSU.Tile.getNeighbors(myTile))
				{
					if (nextTile.IsEmpty)
					{
						combinedFraction += this.m.FractionPerEmptyTile;
					}
				}
			}
		}
		else
		{
			combinedFraction += (_emptyTilesOverwrite * this.m.FractionPerEmptyTile);
		}

		local actor = this.getContainer().getActor();

		// Switcheroo of CurrentProperties, so that dodge is taking into account the most up-to-date initiative values from the current update cycle
		// Otherwise effects like Dazed or Distracted will not update the dodge values currently until another update-cycle is triggered on the character
		local oldCurrentProperties = actor.m.CurrentProperties;
		if (_propertiesOverwrite != null)
		{
			actor.m.CurrentProperties = _propertiesOverwrite;
		}
		local defenseValue = ::Math.floor(actor.getInitiative() * combinedFraction);
		actor.m.CurrentProperties = oldCurrentProperties;

		return ::Math.max(0, defenseValue);
	}
});
