::Hardened.HooksMod.hook("scripts/entity/tactical/actor", function(q) {
	// Vanilla Fix: Overwrite, because Vanilla assumes that if the actor isPlacedOnMap, that then the TurnSequenceBar exists
	// 	That however is not the case during a very small window after combat. If a player update happens during that window, then we get errors
	//	In Vanilla that never happens. But in Hardened we restore equipped items during exactly this window, causing such errors, if not for this fix
	q.setDirty = @() function( _value )
	{
		if (!this.m.IsAlive || this.m.IsDying) return;

		this.updateOverlay();
		this.m.ContentID = ::Math.rand() + ::Math.rand();

		if (this.isPlacedOnMap() && ::Tactical.TurnSequenceBar != null)	// The != null check is new, compared to vanillas implementation
		{
			if (this.m.IsActingEachTurn && ::Tactical.TurnSequenceBar.getActiveEntity() == this)
			{
				this.m.IsDirty = _value;
			}
			else if (_value)
			{
				::Tactical.TurnSequenceBar.updateEntity(this.getID());
				this.m.IsDirty = false;
			}
		}
		else
		{
			this.m.IsDirty = true;
		}
	}

	// Vanilla Fix: We add a "roundToDec" into the calculation, because multiplications like 120 * 1.05 produce 125.999992, which is falsely floored to 15 then
	q.getHitpointsMax = @() function()
	{
		return ::Math.floor(::MSU.Math.roundToDec(this.m.CurrentProperties.Hitpoints * (this.m.CurrentProperties.HitpointsMult >= 0 ? this.m.CurrentProperties.HitpointsMult : 1.0 / this.m.CurrentProperties.HitpointsMult), 3));
	}
});
